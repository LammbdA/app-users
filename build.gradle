buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://repo.enonic.com/public'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath 'com.enonic.gradle:gradle-defaults:1.0.1'
    }
}

plugins {
    id "com.moowork.node" version "1.1.1"
}

apply plugin: 'com.enonic.xp.app'
apply plugin: 'com.enonic.defaults'
apply plugin: 'maven'

app {
    name = project.appName
    displayName = 'Users'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-value:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.graphql-java:graphql-java:2.3.0"
    include "com.enonic.lib:graphql:0.5.2"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.enonic.com/public'
    }
}

node {
    download = true
    version = '6.9.2'
}

ext {
    resourcesDir = file( 'src/main/resources' )
    buildResourcesDir = file( "${buildDir}/resources/main" )
}

task webpack( type: NodeTask, dependsOn: npmInstall ) {
    description = 'Build UI resources (ts, css, etc).'
    inputs.dir 'src/main'
    outputs.dir "${buildDir}/resources/main"
    script = file( './node_modules/webpack/bin/webpack' )
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

jar.dependsOn += webpack
jar.outputs.dir "${buildDir}/resources/main"
