plugins {
    id 'java'
    id 'maven'
    id 'com.enonic.defaults' version '1.0.3'
    id 'com.enonic.xp.app' version '1.0.8'
    id "com.moowork.node" version "1.1.1"
}

group = 'com.enonic.app'
version = '0.1.0-SNAPSHOT'

xp {
    version = '6.11.0'
}

app {
    name = 'com.enonic.app.users'
    displayName = 'Users'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

dependencies {
    compile "com.enonic.xp:core-api:${xp.version}"
    compile "com.enonic.xp:portal-api:${xp.version}"
    include "com.enonic.xp:lib-node:${xp.version}"
    include "com.enonic.xp:lib-portal:${xp.version}"
    include "com.enonic.xp:lib-admin:${xp.version}"
    include "com.enonic.xp:lib-auth:${xp.version}"
    include "com.enonic.xp:lib-mustache:${xp.version}"
    include "com.enonic.xp:lib-io:${xp.version}"
    include "com.enonic.xp:lib-value:${xp.version}"
    include "com.enonic.xp:lib-i18n:${xp.version}"
    include "com.graphql-java:graphql-java:2.3.0"
    include "com.enonic.lib:graphql:0.5.2"
    include 'com.enonic.lib:lib-admin-ui:1.0.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
}

node {
    download = true
    version = '6.9.2'
}

ext {
    resourcesDir = file( 'src/main/resources' )
    buildResourcesDir = file( "${buildDir}/resources/main" )
}

task webpack( type: NodeTask, dependsOn: npmInstall ) {
    description = 'Build UI resources (ts, css, etc).'
    inputs.dir 'src/main'
    outputs.dir "${buildDir}/resources/main"
    script = file( './node_modules/webpack/bin/webpack' )
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

jar.dependsOn += webpack
jar.outputs.dir "${buildDir}/resources/main"
