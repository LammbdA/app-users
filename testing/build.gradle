import com.enonic.uitest.server.ServerInstance

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
            jcenter()
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-gulp-plugin:0.13'
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.0.1'
    id 'com.moowork.node' version '0.13'
    id 'com.moowork.gulp' version '0.13'
    id 'com.github.psxpaul.execfork' version '0.1.8'
}

group = 'com.enonic.xp.js_testing'
version = '1.0'

configurations {
    distro
}

apply plugin: 'com.enonic.defaults'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'

enonic {
    publishRepo = 'inhouse'
}

ext {
    xpVersion = '7.0.0-SNAPSHOT'
    unpackDir = "$buildDir${File.separator}install"
    screenshotsDir = "$buildDir${File.separator}screenshots"
    reportsDir = "$buildDir${File.separator}reports"
    distroDir = "$unpackDir${File.separator}enonic-xp-generic-$xpVersion"
    deployDir = "$distroDir${File.separator}home${File.separator}deploy"
    mochaResultsDir = "mochawesome-report"
    testDataDir = "$projectDir${File.separator}test-data${File.separator}common-config"
    resDir = "$projectDir${File.separator}test-data"
    xpHome = "$distroDir${File.separator}home"
    configDir = "$xpHome${File.separator}config"
    appDir = "$projectDir${File.separator}test-data${File.separator}test-applications"
    xpServer = new ServerInstance()
}
task copyConfig( type: Copy ) {
    println testDataDir
    from testDataDir
    include '**.*.cfg'
    include '*.properties'
    into file( configDir )
}
task copyApps( type: Copy ) {
    println '*********************************************************'
    println '* task started:copyApps  *'
    println '*********************************************************'
    println appDir
    from file( appDir )
    include '*.jar'
    into file( deployDir )
}
dependencies {
    compile "com.enonic.xp:core-api:$xpVersion"
    distro "com.enonic.xp:enonic-xp-generic:$xpVersion@zip"
}
task ensureScreenshotsDirectory {
    doLast {
        mkdir screenshotsDir
    }
}
task deleteFolders( type: Delete ) {
    delete unpackDir, screenshotsDir, reportsDir
}

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into file( unpackDir )
}

task zipScreenshots( type: Zip ) {
    from screenshotsDir
    include '*'
    include '*/*'
    classifier 'screenshots'
}

task zipReport( type: Zip ) {
    from mochaResultsDir
    include '*'
    include '*/*'
    classifier 'mochaReport'
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            artifact zipScreenshots
            artifact zipReport
        }
    }
}

jar.enabled = true

task startServer( dependsOn: [unpackDistro, copyConfig] ) {
    doLast {
        xpServer.installDir = file( distroDir )
        xpServer.startupDelay = 40000
        xpServer.start()
    }
    copyConfig.mustRunAfter unpackDistro
}

task stopServer {
    doLast {
        xpServer.stop()
    }
}

tasks.withType( Test ) {
    jvmArgs '-Xms128m', '-Xmx1024m'
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }
}

task startSeleniumDaemon( type: com.github.psxpaul.task.ExecFork ) {
    println 'Selenium server is starting!'
    commandLine = "${resDir}${File.separator}selenium-server.sh"
    standardOutput = "${buildDir}${File.separator}selenium-daemon.log"
    errorOutput = "${buildDir}${File.separator}selenium-daemon-error.log"
}

task testUsersApp( type: NpmTask, dependsOn: [npmInstall, startServer, copyApps, startSeleniumDaemon, ensureScreenshotsDirectory] ) {
    inputs.files fileTree( dir: '.' )
    outputs.files fileTree( dir: '/test-results' )
    args = ['test']
    startServer.mustRunAfter npmInstall
    copyApps.mustRunAfter startServer
    startSeleniumDaemon.mustRunAfter copyApps
    ensureScreenshotsDirectory.mustRunAfter startSeleniumDaemon
    finalizedBy stopServer
}

task testUsersAppLocally( type: NpmTask, dependsOn: [npmInstall, ensureScreenshotsDirectory, startSeleniumDaemon] ) {
    inputs.files fileTree( dir: '.' )
    outputs.files fileTree( dir: '/test-results' )
    args = ['test']
    startSeleniumDaemon.mustRunAfter npmInstall
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.enonic.com/public'
    }
}
